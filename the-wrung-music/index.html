<!doctype html>
<html lang="en">
	<head>
		<title>The Wrung Music - Codevember 2017</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">

		<link rel="stylesheet" type="text/css" href="../css/styles.css" media="screen" />
		<link href="https://fonts.googleapis.com/css?family=Roboto|Roboto+Slab" rel="stylesheet">

<style>
		#play{
	position: absolute;
	left: 0; right: 0;
	bottom: 0; top: 0;
	width: 100%; height: 100%;
	background-color: black;
	text-align:center;
	line-height: 0;
	padding-top: 50%;
}
@media only screen and (max-width: 650px) {
	.odeo-player{bottom:55px !important;}
}
	</style>

	</head>
	<body>

		<div id="container"></div>
		<div id="info" >
			<p><a href="../digital-analog-color-display" >Previous</a><!-- | <a href="../n" >Next</a>--></p>
			<h1>15. The Wrung Music</h1>
			<p>Another music visualisation.</p>
			<p>Music from <a href="https://soundcloud.com/datashat/helvetica-calcium" >Helvetica Calcium</a> by Datassette.</p>
			<p><a href="#" id="usemic" >Click here to use microphone</a></p>
			<p><b>Click or tap and drag</b> to move the camera, <b>scroll or zoom with two fingers</b> to zoom in and out.</p>
			<p id="moreDetails"><b>More details...</b></p>
		</div>
		<div id="play" >Click to start</div>
		<div id="details" class="hidden">
			<div class="content" >
				<h1>Details</h1>
				<p>Another music visualisation.</p>
				<p>This one spreads the FFT values towards the upper part of the spectrum. Uses particles to render concentric circles that are disturbed based on the FFT and some combinations of perlin noise. In the end, the domain is warped a bit to create a more psychedelic effect.</p>
				<p>The bloom pass is modified to add some slight glow blended in Overlay mode.</p>
				<p>Beat detection and levels by <a href="https://twitter.com/felixturner" >Felix Turner</a>.</p>
				<p>Music from <a href="https://soundcloud.com/datashat/helvetica-calcium" >Helvetica Calcium</a> by Datassette.</p>
				<p>Built with <a href="threejs.org">three.js</a>.</p>
				<p>Code available on <a href="https://github.com/spite/codevember-2017/tree/master/the-wrung-music" >GitHub</a>.</p>
				<a href="#" id="close-details">Close</a>
			</div>
		</div>

		<script src="../js/Common.js"></script>
		<script src="../js/three.min.js"></script>
		<script src="../js/OrbitControls.js"></script>
		<script src="https://connect.soundcloud.com/sdk/sdk-3.1.2.js"></script>
		<script src="../js/THREE.FBOHelper.js"></script>
		<script src="../js/Odeo.js"></script>
		<script src="../js/THREE.PingPongTexture.js"></script>
		<script src="../js/isMobile.min.js"></script>
		<script src="../js/Bloom.js"></script>

<script id="highlight-fs" type="x-shader/x-fragment">
precision highp float;

uniform sampler2D source;
uniform float threshold;

varying vec2 vUv;

void main() {
	vec4 c = texture2D( source, vUv );
	gl_FragColor = vec4( 2. * c.xyz, 1. );
}
</script>

<script id="blur-fs" type="x-shader/x-fragment">
precision highp float;

uniform vec2 resolution;
uniform sampler2D source;
uniform vec2 delta;

varying vec2 vUv;

vec4 blur5(sampler2D image, vec2 uv, vec2 resolution, vec2 direction) {
  vec4 color = vec4(0.0);
  vec2 off1 = vec2(1.3333333333333333) * direction;
  color += texture2D(image, uv) * 0.29411764705882354;
  color += texture2D(image, uv + (off1 / resolution)) * 0.35294117647058826;
  color += texture2D(image, uv - (off1 / resolution)) * 0.35294117647058826;
  return color;
}

vec4 blur9(sampler2D image, vec2 uv, vec2 resolution, vec2 direction) {
  vec4 color = vec4(0.0);
  vec2 off1 = vec2(1.3846153846) * direction;
  vec2 off2 = vec2(3.2307692308) * direction;
  color += texture2D(image, uv) * 0.2270270270;
  color += texture2D(image, uv + (off1 / resolution)) * 0.3162162162;
  color += texture2D(image, uv - (off1 / resolution)) * 0.3162162162;
  color += texture2D(image, uv + (off2 / resolution)) * 0.0702702703;
  color += texture2D(image, uv - (off2 / resolution)) * 0.0702702703;
  return color;
}

vec4 blur13(sampler2D image, vec2 uv, vec2 resolution, vec2 direction) {
  vec4 color = vec4(0.0);
  vec2 off1 = vec2(1.411764705882353) * direction;
  vec2 off2 = vec2(3.2941176470588234) * direction;
  vec2 off3 = vec2(5.176470588235294) * direction;
  color += texture2D(image, uv) * 0.1964825501511404;
  color += texture2D(image, uv + (off1 / resolution)) * 0.2969069646728344;
  color += texture2D(image, uv - (off1 / resolution)) * 0.2969069646728344;
  color += texture2D(image, uv + (off2 / resolution)) * 0.09447039785044732;
  color += texture2D(image, uv - (off2 / resolution)) * 0.09447039785044732;
  color += texture2D(image, uv + (off3 / resolution)) * 0.010381362401148057;
  color += texture2D(image, uv - (off3 / resolution)) * 0.010381362401148057;
  return color;
}

void main() {
	vec4 b  = blur13( source, vUv, resolution, delta );
	gl_FragColor = b;
}
</script>

<script id="bloom-fs" type="x-shader/x-fragment">
precision highp float;

uniform sampler2D base;
uniform sampler2D level0;
uniform sampler2D level1;
uniform sampler2D level2;
uniform sampler2D level3;
uniform sampler2D level4;

uniform vec2 resolution;
uniform float boost;
uniform float reduction;
uniform float levels;
uniform float time;
uniform float amount;

varying vec2 vUv;

#define FXAA_SPAN_MAX 8.0
#define FXAA_REDUCE_MUL   (1.0/FXAA_SPAN_MAX)
#define FXAA_REDUCE_MIN   (1.0/128.0)
#define FXAA_SUBPIX_SHIFT (1.0/4.0)
vec3 FxaaPixelShader( vec4 uv, sampler2D tex, vec2 rcpFrame) {
    vec3 rgbNW = texture2D(tex, uv.zw ).xyz;
    vec3 rgbNE = texture2D(tex, uv.zw + vec2(1,0)*rcpFrame.xy ).xyz;
    vec3 rgbSW = texture2D(tex, uv.zw + vec2(0,1)*rcpFrame.xy ).xyz;
    vec3 rgbSE = texture2D(tex, uv.zw + vec2(1,1)*rcpFrame.xy ).xyz;
    vec3 rgbM  = texture2D(tex, uv.xy ).xyz;
    vec3 luma = vec3(0.299, 0.587, 0.114);
    float lumaNW = dot(rgbNW, luma);
    float lumaNE = dot(rgbNE, luma);
    float lumaSW = dot(rgbSW, luma);
    float lumaSE = dot(rgbSE, luma);
    float lumaM  = dot(rgbM,  luma);
    float lumaMin = min(lumaM, min(min(lumaNW, lumaNE), min(lumaSW, lumaSE)));
    float lumaMax = max(lumaM, max(max(lumaNW, lumaNE), max(lumaSW, lumaSE)));
    vec2 dir;
    dir.x = -((lumaNW + lumaNE) - (lumaSW + lumaSE));
    dir.y =  ((lumaNW + lumaSW) - (lumaNE + lumaSE));
    float dirReduce = max(
        (lumaNW + lumaNE + lumaSW + lumaSE) * (0.25 * FXAA_REDUCE_MUL),
        FXAA_REDUCE_MIN);
    float rcpDirMin = 1.0/(min(abs(dir.x), abs(dir.y)) + dirReduce);
    dir = min(vec2( FXAA_SPAN_MAX,  FXAA_SPAN_MAX),
          max(vec2(-FXAA_SPAN_MAX, -FXAA_SPAN_MAX),
          dir * rcpDirMin)) * rcpFrame.xy;
    vec3 rgbA = (1.0/2.0) * (
        texture2D(tex, uv.xy + dir * (1.0/3.0 - 0.5) ).xyz +
        texture2D(tex, uv.xy + dir * (2.0/3.0 - 0.5) ).xyz);
    vec3 rgbB = rgbA * (1.0/2.0) + (1.0/4.0) * (
        texture2D(tex, uv.xy + dir * (0.0/3.0 - 0.5) ).xyz +
        texture2D(tex, uv.xy + dir * (3.0/3.0 - 0.5) ).xyz);
    float lumaB = dot(rgbB, luma);
    if((lumaB < lumaMin) || (lumaB > lumaMax)) return rgbA;
    return rgbB;
}

float random(vec2 n, float offset ){
	return .5 - fract(sin(dot(n.xy + vec2( offset, 0. ), vec2(12.9898, 78.233)))* 43758.5453);
}

float luma(vec3 color) {
  return dot(color, vec3(0.299, 0.587, 0.114));
}

float luma(vec4 color) {
  return dot(color.rgb, vec3(0.299, 0.587, 0.114));
}

float blendOverlay(float base, float blend) {
	return base<0.5?(2.0*base*blend):(1.0-2.0*(1.0-base)*(1.0-blend));
}

vec3 blendOverlay(vec3 base, vec3 blend) {
	return vec3(blendOverlay(base.r,blend.r),blendOverlay(base.g,blend.g),blendOverlay(base.b,blend.b));
}

vec3 blendOverlay(vec3 base, vec3 blend, float opacity) {
	return (blendOverlay(base, blend) * opacity + base * (1.0 - opacity));
}


void main() {
	vec2 res = 1. / resolution;
	vec2 uv = vUv;
	vec4 aauv = vec4( uv, uv - (res * (0.5 + FXAA_SUBPIX_SHIFT)));
	vec4 color = vec4(FxaaPixelShader( aauv, base, res ),1.);

	/*vec4 blur = vec4(0.);
	if( levels > 0. ) blur += texture2D( level0, vUv );
	if( levels > 1. ) blur += texture2D( level1, vUv );
	if( levels > 2. ) blur += texture2D( level2, vUv );
	if( levels > 3. ) blur += texture2D( level3, vUv );
	if( levels > 4. ) blur += texture2D( level4, vUv );*/
	vec4 blur = texture2D( level4, vUv );

	//blur /= 5.;
	color.xyz = blendOverlay(color.xyz, blur.xyz);
	color += .5 * blur;

	/*vec2 position = vUv - .5;
	float vignette = length( position );
    vignette = boost - vignette * reduction;

 	//color += vec4( vec3( amount * random( vUv, time ) ), 1. );
    color.rgb *= vignette;*/

	gl_FragColor = color;
	//gl_FragColor = vec4(luma(color));
}
</script>

<script type="x-shader/x-vertex" id="ortho-vs" >
precision highp float;

attribute vec3 position;
attribute vec2 uv;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;

varying vec2 vUv;

void main() {
	vUv = uv;
	gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1. );
}
</script>

<script type="x-shader/x-vertex" id="spreadfft-fs" >
precision highp float;

uniform sampler2D fft;
uniform sampler2D source;
uniform vec2 dimensions;
uniform float beat;

varying vec2 vUv;

void main() {
	float inc = 1./dimensions.x;
	float v = texture2D(fft,vUv).r;
	if( vUv.x <= .5 && beat == 1. ) v *= 2.;
	float v2 = texture2D(source,vUv-vec2(inc,0.)).r;
	if( vUv.x <= inc ) v2 = 0.;
	v2 *= .9;
	gl_FragColor = vec4(v + v2);
}
</script>

<script type="x-shader/x-vertex" id="vertexshader">

uniform sampler2D fftMap;
uniform float time;
uniform float beat;

varying vec3 vColor;

vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec2 mod289(vec2 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec3 permute(vec3 x) {
  return mod289(((x*34.0)+1.0)*x);
}

float snoise(vec2 v)
  {
  const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626,  // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
// First corner
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);

// Other corners
  vec2 i1;
  //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
  //i1.y = 1.0 - i1.x;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  // x0 = x0 - 0.0 + 0.0 * C.xx ;
  // x1 = x0 - i1 + 1.0 * C.xx ;
  // x2 = x0 - 1.0 + 2.0 * C.xx ;
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;

// Permutations
  i = mod289(i); // Avoid truncation effects in permutation
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
		+ i.x + vec3(0.0, i1.x, 1.0 ));

  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;

// Gradients: 41 points uniformly over a line, mapped onto a diamond.
// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;

// Normalise gradients implicitly by scaling m
// Approximation of: m *= inversesqrt( a0*a0 + h*h );
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

// Compute final noise value at P
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}

vec3 hsv2rgb(vec3 c) {
  vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
  vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
  return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

const float PI = 3.14159265359;

void main() {

	float a = position.x;
	float r = position.y;

	float h = texture2D( fftMap, .1 + .9 * vec2(r/500.,0.)).r;

	vec3 p = vec3( r * cos( a ), 0., r * sin( a ) );
	r += 40. * snoise(.001*h*p.xz+time);// * beat;
	a += .5 * PI * snoise(.001*h*p.xz+time);
	p += vec3( r * cos( a + .01 * time ), 0., r * sin( a + .01 * time ) );

	p.y += 40. * h;

	p.y += 5. * snoise(.01 *p.xz + vec2(time,0.)) * beat;
	p.y += h * 2. * snoise(.005*p.xz + vec2(.4 * time,0.));
	p.y += h * 2. * snoise(.001*p.xz + vec2(.4 * time,0.));
	p.y += h * 4. * snoise(.0005*p.xz + vec2(.4 * time,0.));

	vec4 mvPosition = modelViewMatrix * vec4( p, 1.0 );

	vColor = hsv2rgb(vec3(a/(2.*PI),1.,1.-r/500.));
	float size = 40.;
	gl_PointSize = size * ( 300.0 / -mvPosition.z );

	gl_Position = projectionMatrix * mvPosition;

}

</script>

<script type="x-shader/x-fragment" id="fragmentshader">

varying vec3 vColor;

void main() {

	float d = 2. * length( .5 - gl_PointCoord );
	float a = 1.-d;
	a *= .1;
	gl_FragColor = vec4(vColor * 1.5,a);

}

</script>
<script>

'use strict';

var container, renderer, camera, controls, scene;
var mesh, bloom, baseFBO;

var helper;

var container = document.getElementById( 'container' );

var odeo = new Odeo( {
	soundCloudId: 'b1275b704badf79d972d51aa4b92ea15'
} );

var playDiv = document.getElementById( 'play' );

if( isMobile.any ) {

	function startPlaying() {
		playDiv.removeEventListener( 'click', startPlaying )
		odeo.playSoundCloud('https://soundcloud.com/datashat/helvetica-calcium')
		playDiv.style.display = 'none';
	}
	playDiv.addEventListener( 'click', startPlaying )

} else {
	playDiv.style.display = 'none';
	odeo.playSoundCloud('https://soundcloud.com/datashat/helvetica-calcium')
}

document.getElementById( 'usemic' ).addEventListener( 'click', function() {
	odeo.stopSoundCloud();
	odeo.useMicrophone();
});

document.body.appendChild(odeo.getUI());

var particles;
var fftSpreadTexture;
var floatType = isMobile.any?THREE.HalfFloatType:THREE.FloatType;

function initScene() {

	baseFBO = new THREE.WebGLRenderTarget( 1, 1, {
		wrapS: THREE.ClampToEdgeWrapping,
		wrapT: THREE.ClampToEdgeWrapping,
		format: THREE.RGBAFormat,
		type: THREE.UnsignedByteType,
		minFilter: THREE.LinearFilter,
		magFilter: THREE.LinearFilter,
		stencilBuffer: false,
		depthBuffer: false
	});

	baseFBO.generateMipMaps = false;

	helper.attach( baseFBO, 'base' );

	var fftTexture = odeo.getSpectrumTexture();
	fftTexture.wrapS = fftTexture.wrapT = THREE.ClampToEdgeWrapping;

	helper.attach( fftTexture, 'fft' );

	var spreadShader = new THREE.RawShaderMaterial( {
		uniforms:{
			fft: { value: fftTexture },
			source: { value: fftTexture },
			beat: { value: 0 },
			dimensions: { value: new THREE.Vector2( fftTexture.width, .5 * fftTexture.width ) }
		},
		vertexShader: document.getElementById( 'ortho-vs' ).textContent,
		fragmentShader: document.getElementById( 'spreadfft-fs' ).textContent
	} );

	fftSpreadTexture = new PingPongTexture( renderer, spreadShader, fftTexture.width, fftTexture.height,THREE.RGBAFormat, THREE.FloatType )

	helper.attach( fftSpreadTexture.front, 'fft spread front' );
	helper.attach( fftSpreadTexture.back, 'fft spread back' );

	var vertices = [];
	var vertex = new THREE.Vector3();
	var circles = 100;

	for ( var i = 0; i < circles; i ++ ) {

		var r = i * 5;
		var diameter = 2 * Math.PI * r;
		var steps = diameter;
		var step = 2 * Math.PI / steps;
		for( var j = 0; j < 2 * Math.PI; j += step ) {
			vertex.set( r * Math.cos(j), 0, r * Math.sin(j));
			vertices.push( j );
			vertices.push( r );
			vertices.push( 0 );
		}

	}

	var positions = new Float32Array( vertices );

	var geometry = new THREE.BufferGeometry();
	geometry.addAttribute( 'position', new THREE.BufferAttribute( positions, 3 ) );

	var material = new THREE.ShaderMaterial( {
		uniforms: {
			fftMap: { value: fftTexture },
			time: { value: 0 },
			beat: { value: 0 },
		},
		vertexShader: document.getElementById( 'vertexshader' ).textContent,
		fragmentShader: document.getElementById( 'fragmentshader' ).textContent,
		transparent: true,
		depthTest: false,
		depthWrite: false,
		blending: THREE.AdditiveBlending
	} );

	particles = new THREE.Points( geometry, material );
	scene.add( particles );

	odeo.onBeat = function() {
		spreadShader.uniforms.beat.value = 1;
		material.uniforms.beat.value = 1;
	}

	bloom = new Bloom();
	bloom.highlightShader.uniforms.threshold.value = .9;

}

function init() {

	container = document.getElementById( 'container' );

	scene = new THREE.Scene();

	camera = new THREE.PerspectiveCamera( 70, window.innerWidth / window.innerHeight, .1, 10000 );
	camera.target = new THREE.Vector3( 0, 0, 0 );
	camera.position.set( 300,800,300 );
	camera.lookAt( camera.target );
	scene.add( camera );

	renderer = new THREE.WebGLRenderer( { antialias: true, preserveDrawingBuffer: true } );
	renderer.setPixelRatio( window.devicePixelRatio );
	renderer.setClearColor( 0, 0 );
	container.appendChild( renderer.domElement );

	helper = new FBOHelper(renderer);
	helper.show(false);

	addCommonUI(renderer.domElement);

	renderer.shadowMap.enabled = true;
	renderer.shadowMap.type = THREE.PCFShadowMap;

	controls = new THREE.OrbitControls( camera, renderer.domElement );

	initScene();
	onWindowResized();

	window.addEventListener( 'resize', onWindowResized );

	window.addEventListener( 'mousedown', _ => fftSpreadTexture.shader.uniforms.beat.value = 1 );
	window.addEventListener( 'mouseup', _ => fftSpreadTexture.shader.uniforms.beat.value = 0 );

	animate();

}

function onWindowResized( event ) {

	var w = container.clientWidth;
	var h = container.clientHeight;

	renderer.setSize( w, h );
	camera.aspect = w / h;
	camera.updateProjectionMatrix();

	var dPR = window.devicePixelRatio;
	bloom.setSize( w * dPR, h * dPR );
	baseFBO.setSize(w * dPR, h * dPR );
	helper.refreshFBO(baseFBO );

	helper.setSize(w,h);
}

function animate() {

	requestAnimationFrame( animate );

	controls.update();
	odeo.update();

	particles.material.uniforms.time.value += .1 * particles.material.uniforms.beat.value;

	fftSpreadTexture.render();
	particles.material.uniforms.fftMap.value = fftSpreadTexture.front.texture;

	renderer.render( scene, camera, baseFBO, true );
	bloom.render();

	helper.update();

	fftSpreadTexture.shader.uniforms.beat.value *= .9;
	particles.material.uniforms.beat.value *= .9;

}

window.addEventListener( 'load', init );

</script>

	</body>
</html>
